apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "im.adamant.android"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode getVersionCode(minSdkVersion, scmVersion.version)
        versionName scmVersion.version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        lintOptions {
            abortOnError false
        }
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        buildConfigField "int", "MAX_ERRORS_FOR_CHANGE_NODE", "3"
        buildConfigField "int", "PING_SECONDS_DELAY", "4"
        buildConfigField "int", "UPDATE_BALANCE_SECONDS_DELAY", "30"
        buildConfigField "String", "API_BASE", "\"/api/\""
        buildConfigField "String", "WELCOME_MESSAGE_ADDR", "\"U15423595369615486571\""
        buildConfigField "String", "MESSAGE_CTNCR_ADDR", "\"U7047165086065693428\""

    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDirs = ['src/main/jniLibs']
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "debug") {
                outputFileName = "adamant-v" + scmVersion.version + "(" + defaultConfig.versionCode + ")" + ".apk"
            } else {
                outputFileName = "adamant-" + variant.buildType.name + ".apk"
            }
        }
    }
}

scmVersion {
    localOnly = true
}

static def getVersionCode(minSdk, versionName) {
    def (cleanVersion, other) = versionName.tokenize('-')
    def (major, minor, path) = cleanVersion.tokenize('.')
    return minSdk.apiLevel * 10000000 + major.toInteger() * 10000 + minor.toInteger() * 100 + path.toInteger()
}

ext {
    supportLibraryVersion = "27.1.1"
    moxyVersion = "1.4.6"
    daggerVersion = "2.16"
    butterKnifeVersion = "8.8.1"
    retrofitVersion = "2.4.0"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"
    implementation "net.java.dev.jna:jna:4.5.1@aar"
    implementation "com.goterl.lazycode:lazysodium-android:2.2.0@aar"
    implementation "io.github.novacrypto:BIP39:0.1.9"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "com.arello-mobile:moxy:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
    implementation "ru.terrakok.cicerone:cicerone:3.0.0"
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    implementation "com.github.curioustechizen.android-ago:library:1.4.0"
    implementation "com.facebook.shimmer:shimmer:0.1.0@aar"
    implementation "me.dm7.barcodescanner:zxing:1.9.8"
    implementation "com.github.kenglxn.QRGen:android:2.5.0"
    implementation "gun0912.ted:tedpermission:2.2.0"
    implementation "com.github.stealthcopter:AndroidNetworkTools:0.4.0"
    implementation "com.android.support:transition:27.1.1"
    implementation "com.github.franmontiel:LocaleChanger:0.9.2"
    implementation "com.andrognito.pinlockview:pinlockview:2.1.0"
    implementation 'com.android.support:multidex:1.0.3'

    annotationProcessor "com.arello-mobile:moxy-compiler:$moxyVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'
}
